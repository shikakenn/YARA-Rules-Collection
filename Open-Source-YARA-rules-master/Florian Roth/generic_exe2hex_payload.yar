/*
    Yara Rule Set
    Author: Florian Roth
    Date: 2016-01-15
    Identifier: Exe2hex
*/

rule Payload_Exe2Hex {
    meta:
        id = "5en7mQdkEDSGy8SxACVzgO"
        fingerprint = "v1_sha256_91b738f0174a267bbc900d59abcb504d2ae0bac8c287c3b7d1ebfc57374a7ee7"
        version = "1.0"
        score = 70
        date = "2016-01-15"
        modified = "2025-03-10"
        status = "RELEASED"
        sharing = "TLP:WHITE"
        source = "YARA-RULES-COLLECTION"
        author = "Florian Roth"
        description = "Detects payload generated by exe2hex"
        category = "INFO"
        reference = "https://github.com/g0tmi1k/exe2hex"

    strings:
        $a1 = "set /p \"=4d5a" ascii
        $a2 = "powershell -Command \"$hex=" ascii
        $b1 = "set+%2Fp+%22%3D4d5" ascii
        $b2 = "powershell+-Command+%22%24hex" ascii
        $c1 = "echo 4d 5a " ascii
        $c2 = "echo r cx >>" ascii
        $d1 = "echo+4d+5a+" ascii
        $d2 = "echo+r+cx+%3E%3E" ascii
    condition:
        all of ($a*) or all of ($b*) or all of ($c*) or all of ($d*)
}
